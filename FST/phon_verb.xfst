define cons [б|в|г|д|ж|й|з|к|л|м|н|п|р|с|т|ф|х|ц|ч|ш|щ|ҕ|ҥ|һ|ь|h|һ];
define vows [а|и|е|ё|о|ы|э|ю|я|ө]; !not ү and у

define allVowels [а|и|е|ё|о|ы|э|ю|я|ө|ү|у];
define highVowels [и|ы|ү|у];
define lowVowels [а|о|э|ө];

define suffixbeg [р|г|л];

define labvow [о|у|ө|ү];
define nonlabvow [а|ы|э|и];

!define diphthongs [ {уо} | {үө} | {ыа} | {иэ} ];

define laterals л;
define rhotic [й|р];

define voiceless [к|в|д|с|т|з|ж|п|х];
!define uvular х;
define nasal [м|н|ҥ];

define phonG %{G%} -> ҕ || cons "^" ?* _ ;
define phonGrest %{G%} -> г || allVowels "^" ?* _ ;

define dipht %{DF%} -> {иэ} || [.#. cons [{иэ}|э+] | .#. [{иэ}|э+] ] ?* _ ,,
             %{DF%} -> {уо} || [.#. cons [о|{уо}] | .#. [о|{уо}] ] ?* _ ,,
             %{DF%} -> {үө} || [.#. cons ө | .#. ө ] ?* _ ,,
             %{DF%} -> {ыа} || [.#. cons ы | .#. ы ] ?* _ ;

define diphtAlter р "^" {иэ} -> {рдиэ} ,,
                  {ҕус} "^" {уо} -> {хсуо} ,,
                  {ээ} "^" %{DF%} -> {иэ},,
                  {аа} "^" %{DF%} -> {ыа};

define phonI  %{I%} -> ы || [.#. cons [ы|а|{уһ}|{ул}] | .#. [а|у|ы] ] ?* _ ,,
              %{I%} -> и || [.#. [{үө}|и|э|ү cons]| .#. ? [{үө}|и|э|{өл}|{үр}] ] ?* _  ,,
              %{I%} -> у || [.#. cons [о|{уу}|{ур}] | .#. о] ?* _  ,,
              %{I%} -> ү || [.#. cons [ ө [voiceless|rhotic]] | .#. ө] ?* _ ,,
                            .#. cons {үс} ?* _  ;             

define phonA %{A%} -> а || [.#. [ы|а|у|{от}|{ор}]| .#. ? [ы|а|у]] ?* _  ,,
             %{A%} -> э || [.#. [и|э|ү cons|{үө}]| .#. ? [и|э|ү|{өл}|{өт}] ] ?* _  ,,
             %{A%} -> ө || [.#. ө| .#. cons ө р] ?* _  ,,
             %{A%} -> о || [.#. [cons о] | .#. {ол}|{оҕ}] ?* _ ;

define phonA2 %{AA%} -> э || [.#. [ө|э|{иэ}]| .#. cons [ө|э|{иэ}]] ?* _  ,,
              %{AA%} -> а || [.#. cons о| .#. о] ?* _  ;
            
define novows  "'" [%{E%}|%{A%}|%{II%}] -> 0 || allVowels "^" ?* _,,
               "'" %{I%} -> 0 || allVowels "^" ?* _;

define phonE %{E%} -> а || [.#. [ы|а|у]| .#. ? [ы|а|у]] ?* _  ,,
             %{E%} -> э || [.#. [и|э|ү cons|{үө}] | .#. ? [и|э|ү]] ?* _  ,,
             %{E%} -> о || [.#. о| .#. cons о] ?* _  ,,
             %{E%} -> ө || [.#. ө| .#. cons ө] ?* _  ,,
              
             {лтаа} "^" л -> {лтуул} ,,
             {иэ} "^" -> {ии} || _ л ?*,,
             с "^" "'" -> һ ;

define phonII %{II%} -> а || [.#. [у|ы]] ?* _   ,,
              %{II%} -> э || [.#. {үө}] ?* _   ,,
              %{II%} -> о || [.#. о] ?* _   ;
              
define phonP %{B%} -> п || voiceless "^" _ ,,
             %{B%} -> б || л "^" _ ,, 
             %{B%} -> м || н "^" _  ; 

define BDefault %{B%} -> б;

define phonT %{T%} -> т || [к|п|с|т|х|а] "^" _ ,,
             %{T%} -> н || [м|н] "^" _ ,,
             %{T%} -> л || [и|л] "^" _ ,,
             %{T%} -> д || [й|р] "^" _  ; 

define Vsvyas %{V%} -> а || [ .#. а| .#. ? у ] ?* _,,
              %{V%} -> э || [.#. {үө}] ?* _;

define Fut %{F%} -> ы || л "^" _;        

define phoninbetween "´" -> 0 || allVowels "^" _ ,,
                     "´" -> ы || [с|п] "^" _ ,,       
                     "´" -> и || р "^" _ ;  
                     
define negation2 %{M%} -> 0 || allVowels "^" _,,
                 %{M%} -> п || х "^" _,, 
                 %{M%} -> м || ҥ "^" _ ;
           
define lharm     %{L%} -> т || [voiceless | cons а |{һаа}| э] "^" _ ,,
                 %{L%} -> н || nasal "^" _ ,,
                 %{L%} -> р || {таа} "^" _ ,,
                 %{L%} -> д || rhotic "^" _  ,,
                 %{L%} -> л || [highVowels | laterals] "^" _  ;

define lharmDefault %{L%} -> л;

define rharmDefault с "^"  -> {һ} || _ {ма} ,,
                    с "^" а -> {һа} || _ ? ,,
                    {иэ} "^" р -> {иир} || ? _ ,,
                    {лээ} "^" л -> {лиил} || ? _  ,,
                    {сээ} "^" р -> {сиир} || ? _  ,,
                    {кээ} "^" р -> {күүр} ||  ? _ ,,
                    {тээ} "^" р -> {тиир} ||  ? _ ;   

define CtoH с "^" ы -> {һы} ,,
            с "^" и -> {һи} ,,
            с "^" э -> {һэ} ,,
            с "^" б -> {сп} ,,
            т "^" п -> {пп} ,,
            "{I}" -> 0 ,,
            {уй} "^" д -> {унн} ,,
            н "^" м -> {мм} ,,
            {лун} "^" б -> {ллуб} ,,
            {лаа} "^" р -> {лыыр} ;  


define except  п "^" ы -> {бы} ,,
               {ээ} "^" ҥ -> {эҥ} ,,
               {наа} "^" ҕ -> {ныаҕ} ,,
              {рыт} "^" "'" {ар} -> {лдьар},,
              {рый} "^" "'" {ар} -> {лдьар},,
              {лээ} "^" р -> {лиир},,
              {ыарый} "^" б -> {ыалдьыб},,
              {ҕҥ} -> {уоҥ} ,,
              {аах} ["^"|"^" "'"] а -> {ааҕа} ,,
              {ыа} "^" р -> {ыыр},,
              {ыа} "^" г -> {ыыг},,
              {ыйаа} "^" б -> {ыйыыб} ,,
              {ҕыһ} -> {хс},,
              {аа} "^" -> {ыы} || _ suffixbeg ?*,,
              {оҕуһ} -> {охс} ,,
              {иһит} -> {ист} ,,
              !{баар} "^" "'" {ал} -> {баал} ,,
              {баарлар} -> {бааллар} ,,
              {ууй} "^" "'" {ар} -> {уунар} ,,
              {һын} "^" б -> {стыб} ;

define cleanup ["^"|"'"|"{II}"] -> 0;


define firstPart  novows .o. phonI .o. phonE .o.  phonII .o. phonA  .o.  
phonA2 .o. Vsvyas .o. phoninbetween .o.  dipht .o. diphtAlter;

define secondPart phonP .o. BDefault .o. phonT .o. phonG .o. phonGrest .o. lharm .o. 
lharmDefault .o. Fut .o. negation2;

define thirdPart rharmDefault .o.  CtoH .o. except .o. cleanup;

regex firstPart .o. secondPart .o. thirdPart;
